# Copyright by The HDF Group. All rights reserved.

# This is the main configure file for the Zstandard filter, an HDF5 plugin
# library that enables Zstandard compression as an HDF5 filter.
# This file is modified from a configure.ac from the hdf5_plugin project.

# Allen Byrne, Ed Hartnett 1/14/19
# Charlie Zender 9/15/20

# Initialize autoconf.
AC_PREREQ(2.59)
AC_INIT(H5ZSTD, 1.0, nco-bugs@lists.sourceforge.net)
AC_CONFIG_HEADER([config.h])
AC_CONFIG_MACRO_DIR([m4])

# Initialize automake.
AM_INIT_AUTOMAKE([foreign])

# Find C compiler.
AC_PROG_CC

AC_PROG_INSTALL

# Initialize libtool, checking for dlopen.
LT_INIT(dlopen)

# If the env. variable HDF5_PLUGIN_PATH is set, or if
# --with-hdf5-plugin-path=<directory>, use it as a place for the large
# (i.e. > 2 GiB) files created during the large file testing.
AC_MSG_CHECKING([where to put HDF5 plugins])
HDF5_PLUGIN_PATH=${HDF5_PLUGIN_PATH-'/usr/local/hdf5/lib/plugin'}
AC_ARG_WITH([hdf5-plugin-path],
            [AS_HELP_STRING([--with-hdf5-plugin-path=<directory>],
                            [specify HDF5 plugin path (defaults to /usr/local/hdf5/lib/plugin, or value of HDF5_PLUGIN_PATH, if set)])],
            [HDF5_PLUGIN_PATH=$with_hdf5_plugin_path])
AC_MSG_RESULT($HDF5_PLUGIN_PATH)
AC_SUBST([HDF5_PLUGIN_PATH])

# Are the Zstandard library and header present?
AC_CHECK_HEADERS([zstd.h], [], [AC_MSG_ERROR([zstd.h is required, set CPPFLAGS.])])
AC_CHECK_LIB([zstd], [ZSTD_compress], [], [AC_MSG_ERROR([libzstd is required, set LDFLAGS.])])

# Modern Zstandard libraries support ZSTD_minCLevel()
AC_CHECK_FUNC([ZSTD_minCLevel],AC_DEFINE([HAVE_ZSTD_MINCLEVEL],1,[Define to 1 if 'ZSTD_minCLevel()' is present]),ccr_have_zstd_minclevel=no)
AC_CHECK_FUNC([ZSTD_maxCLevel],AC_DEFINE([HAVE_ZSTD_MAXCLEVEL],1,[Define to 1 if 'ZSTD_maxCLevel()' is present]),ccr_have_zstd_maxclevel=no)
AC_CHECK_FUNC([ZSTD_getFrameContentSize],AC_DEFINE([HAVE_ZSTD_GETFRAMECONTENTSIZE],1,[Define to 1 if 'ZSTD_getFrameContentSize()' is present]),ccr_have_zstd_getframecontentsize=no)

# We need the math library (20200915 fxm for what?)
AC_CHECK_LIB([m], [floor], [], [AC_MSG_ERROR([Math library is required.])])

# We need the HDF5 headers and library.
AC_CHECK_HEADERS([hdf5.h], [], [AC_MSG_ERROR([hdf5.h is required, set CPPFLAGS.])])
AC_SEARCH_LIBS([H5Fflush], [hdf5dll hdf5], [], [AC_MSG_ERROR([libhdf5 is required, set LDFLAGS.])])

# Check for other header files we need.
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h stdlib.h string.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_ERROR_AT_LINE
AC_FUNC_MEMCMP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([memset])

# Check which plugins to build, if no environmental variables are set, build
# all.
if test ! "$PLUGIN_H5ZSTD"
then
  PLUGIN_H5ZSTD=1
fi
AM_CONDITIONAL(H5ZSTD, test "$PLUGIN_H5ZSTD")

## These files will be generated by configure
AC_CONFIG_FILES([Makefile
        example/Makefile
        src/Makefile])

## Output configure and all Makefile.in files.
AC_OUTPUT
