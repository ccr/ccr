# Copyright by The HDF Group. All rights reserved.

# This is the main configure file for the BITGROOM filter, a HDF5 plugin
# library that enables BitGrooming quantization as an HDF5 filter.
# This file is modified from a configure.ac from the hdf5_plugin project.

# Allen Byrne, Ed Hartnett 1/14/19
# Charlie Zender 9/10/20

# Initialize autoconf.
AC_PREREQ(2.59)
AC_INIT(H5BGR, 1.0, nco-bugs@lists.sourceforge.net)
AC_CONFIG_HEADER([config.h])
AC_CONFIG_MACRO_DIR([m4])

# Initialize automake.
AM_INIT_AUTOMAKE([foreign])

# Find C compiler.
AC_PROG_CC

AC_PROG_INSTALL

# Initialize libtool, checking for dlopen.
LT_INIT(dlopen)

# If the env. variable HDF5_PLUGIN_DIR is set, or if
# --with-hdf5-plugin-dir=<directory>, use it as a place for the large
# (i.e. > 2 GiB) files created during the large file testing.
AC_MSG_CHECKING([where to put HDF5 plugins])
HDF5_PLUGIN_DIR=${HDF5_PLUGIN_DIR-'/usr/local/hdf5/lib/plugin'}
AC_ARG_WITH([hdf5-plugin-dir],
            [AS_HELP_STRING([--with-hdf5-plugin-dir=<directory>],
                            [specify HDF5 plugin directory (defaults to /usr/local/hdf5/lib/plugin, or value of HDF5_PLUGIN_DIR, if set)])],
            [HDF5_PLUGIN_DIR=$with_hdf5_plugin_dir])
AC_MSG_RESULT($HDF5_PLUGIN_DIR)
AC_SUBST([HDF5_PLUGIN_DIR])

# Are the BitGroom library and header present?
# 20200912 fxm: Eventually move quantization library into, e.g., ccr_ppc.[c/h]?
# For now, keep filter source code ccr_bgr() in plugin
#AC_CHECK_HEADERS([nco.h], [], [AC_MSG_ERROR([nco.h is required, set CPPFLAGS.])])
#AC_CHECK_LIB([nco], [nco], [], [AC_MSG_ERROR([libnco is required, set LDFLAGS.])])

# We need the math library
AC_CHECK_LIB([m], [floor], [], [AC_MSG_ERROR([Math library is required.])])

# We need the HDF5 headers and library.
AC_CHECK_HEADERS([hdf5.h], [], [AC_MSG_ERROR([hdf5.h is required, set CPPFLAGS.])])
AC_SEARCH_LIBS([H5Fflush], [hdf5dll hdf5], [], [AC_MSG_ERROR([libhdf5 is required, set LDFLAGS.])])

# Check for other header files we need.
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h math.h stdlib.h string.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_ERROR_AT_LINE
AC_FUNC_MEMCMP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([memset])

# Check which plugins to build, if no environmental variables are set, build
# all.
if test ! "$PLUGIN_H5BGR"
then
  PLUGIN_H5BGR=1
fi
AM_CONDITIONAL(H5BGR, test "$PLUGIN_H5BGR")

## These files will be generated by configure.
# fxm: 20200912 add example directory
#AC_CONFIG_FILES([Makefile
#        example/Makefile
#        src/Makefile])
AC_CONFIG_FILES([Makefile
        src/Makefile])

## Output configure and all Makefile.in files.
AC_OUTPUT
